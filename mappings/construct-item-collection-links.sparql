PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX http: <http://www.w3.org/2011/http#>
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX bsm: <http://w3id.org/besocial/ns/model#>
PREFIX bsd: <http://w3id.org/besocial/data/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX ex: <http://example.org/ns#>



INSERT {
   ?postRepresentation
    dcterms:isPartOf ?collectionRepresentation ;
    dcterms:isPartOf ?collectionRepresentationVersion . 
}
WHERE {

  ?postRepresentation
    a edm:WebResource ;
    dcterms:isPartOf ?response .

  ?response
    a http:Response ;
    dcterms:isPartOf ?warcFile .

  #
  # There are less warc files and collections than posts, query them once and reuse them later
  #
  {
  SELECT ?warcFile ?collectionRepresentationVersion ?collectionRepresentation
  WHERE {
    SERVICE <db://sfm-collections> {
      ?warcFile
        a premis:File ;
        dcterms:isPartOf ?collectionRepresentationVersion .

      ?collectionRepresentationVersion
        prov:specializationOf ?collectionRepresentation .

      ?aggregation
        a edm:Aggregation ;
        edm:hasView ?collectionRepresentation ;
        edm:aggregatedCHO ?collectionCHO .

      ?collectionCHO
        a edm:ProvidedCHO, bsm:SocialMediaCollection .
    }
   }
  }
}

