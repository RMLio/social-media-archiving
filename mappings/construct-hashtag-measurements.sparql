PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX http: <http://www.w3.org/2011/http#>
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX bsm: <http://w3id.org/besocial/ns/model#>
PREFIX bsd: <http://w3id.org/besocial/data/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX ex: <http://example.org/ns#>
PREFIX sioc: <http://rdfs.org/sioc/ns#>

CONSTRUCT {
  
  ?obs
    a qb:Observation ;
    bsm:collectionDimension                         ?collectionCHO ;
    bsm:collectionRepresentationDimension           ?collectionRepresentation ;
    bsm:collectionRepresentationVersionDimension    ?collectionRepresentationVersion ;
    bsm:hashtagDimension                            ?hashtag ;
    bsm:numberPostsWithHashtags                     ?count .
}
WHERE {

  #
  # Select related context of the grouped content: collectionCHO and collectionRepresentation
  #
  ?collectionRepresentationVersion prov:specializationOf ?collectionRepresentation .

  ?aggregationRepresentation
    a edm:Aggregation ;
    edm:hasView ?collectionRepresentation ;
    edm:aggregatedCHO ?collectionCHO .


  #
  # Get the hashtags and how often they occur for each collection representation version (only more than 1 occurrence)
  # 
  {
    SELECT ?collectionRepresentationVersion ?hashtag (count(?hashtag) as ?count)
    WHERE {
      
      ?itemCHO schema:mentions ?hashtag .

      ?hashtag a sioc:Tag .

      ?itemAggregation
        a edm:Aggregation ;
        edm:hasView ?itemRepresentation ;
        edm:aggregatedCHO ?itemCHO .

      ?itemRepresentation
        a edm:WebResource ;
        dcterms:isPartOf ?collectionRepresentationVersion .

      ?collectionRepresentationVersion
        a bsm:SocialMediaCollectionRepresentationVersion .

    }
    GROUP BY ?collectionRepresentationVersion ?hashtag
    HAVING (count(?hashtag) > 1)
  }

  BIND (URI(concat("http://w3id.org/besocial/data/obs_", STRUUID())) as ?obs)
}
