PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX http: <http://www.w3.org/2011/http#>
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX bsm: <http://w3id.org/besocial/ns/model#>
PREFIX bsd: <http://w3id.org/besocial/data/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX ex: <http://example.org/ns#>
PREFIX sioc: <http://rdfs.org/sioc/ns#>

INSERT {
  
  ?obs
    a bsm:HashtagsObservation ;
    bsm:collectionDimension                         ?collectionCHO ;
    bsm:collectionRepresentationDimension           ?collectionRepresentation ;
    bsm:collectionRepresentationVersionDimension    ?collectionRepresentationVersion ;
    bsm:hashtagDimension                            ?hashtag ;
    bsm:numberPostsWithHashtags                     ?count .
}
WHERE {

  BIND (URI(concat("http://w3id.org/besocial/data/obs_", STRUUID())) as ?obs)

  {
    SELECT ?collectionCHO ?collectionRepresentation ?collectionRepresentationVersion ?hashtag (count(?itemAggregation) as ?count)
    WHERE {
      SERVICE<${TRIPLE_STORE_ITEMS}> {
        ?itemAggregation
          a edm:Aggregation ;
          edm:hasView ?itemRepresentation ;
          edm:aggregatedCHO ?itemCHO .

        ?itemRepresentation
          a edm:WebResource ;
          dcterms:isPartOf ?collectionRepresentationVersion .        

        #
        # only count mentioned things which are hashtags
        #
        ?itemCHO
          a schema:SocialMediaPosting ;
          schema:mentions ?hashtag .
     
        ?hashtag a sioc:Tag . 
      }

      #
      # Match found social media posts with their collections
      # so we get needed dimensions to build the observations
      #
      {
        SELECT ?collectionCHO ?collectionRepresentation ?collectionRepresentationVersion
        WHERE {
          hint:SubQuery hint:runOnce true .
          SERVICE <${TRIPLE_STORE_COLLECTIONS}> {

            ?collectionRepresentationVersion
              a bsm:SocialMediaCollectionRepresentationVersion ;
              prov:specializationOf ?collectionRepresentation .

            ?collectionRepresentation
              a bsm:SocialMediaCollectionRepresentation .

            ?collectionAggregation
              edm:hasView ?collectionRepresentation ;
              edm:aggregatedCHO ?collectionCHO .
          }    
        }
      }
    }
    GROUP BY ?collectionCHO ?collectionRepresentation ?collectionRepresentationVersion ?hashtag
    HAVING (count(?hashtag) > 1)
  }
}

