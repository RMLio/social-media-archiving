PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX http: <http://www.w3.org/2011/http#>
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX bsm: <http://w3id.org/besocial/ns/model#>
PREFIX bss: <http://w3id.org/besocial/ns/shapes#>
PREFIX bsd: <http://w3id.org/besocial/data/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX ex: <http://example.org/ns#>

CONSTRUCT {
  
  ?obs
    a bsm:SocialMediaPostsObservation ;
    bsm:collectionDimension                      ?collectionCHO ;
    bsm:collectionRepresentationDimension        ?collectionRepresentation ;
    bsm:collectionRepresentationVersionDimension ?collectionRepresentationVersion ;
    bsm:monthDimension      ?month ;
    bsm:yearDimension       ?year ;
    bsm:numberPosts         ?numberOfPosts .

}
WHERE {

  #
  # Get some context: related concepts to the aggregated data queried in the subquery below
  #
  ?collectionRepresentationVersion
    a edm:WebResource ;
    prov:specializationOf ?collectionRepresentation .

    ?aggregation
      a edm:Aggregation ;
      edm:hasView ?collectionRepresentation ;
      edm:aggregatedCHO ?collectionCHO .

    ?collectionCHO
      a edm:ProvidedCHO .

  #
  # Get the aggregated number of posts per year and month
  #
  {
  SELECT ?collectionRepresentationVersion ?year ?month (COUNT(?itemRepresentation) AS ?numberOfPosts)
  WHERE {
      

      ?itemAggregation
        a edm:Aggregation ;
        edm:hasView ?itemRepresentation ;
        edm:aggregatedCHO ?itemCHO .

      ?itemCHO
        a schema:SocialMediaPosting ;
        dc:created ?creationDate .
        

      ?itemRepresentation
        a edm:WebResource ;
        dcterms:isPartOf ?collectionRepresentationVersion .

  }
  GROUP BY ?collectionRepresentationVersion (year(?creationDate) AS ?year) (month(?creationDate) AS ?month)
  }

  #
  # Generate a new IRI for every observation
  #
  BIND (URI(concat("http://w3id.org/besocial/data/obs_", STRUUID())) as ?obs)
}
