PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX http: <http://www.w3.org/2011/http#>
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX bsm: <http://w3id.org/besocial/ns/model#>
PREFIX bsq: <http://w3id.org/besocial/ns/quality#>
PREFIX bsd: <http://w3id.org/besocial/data/>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX ex: <http://example.org/ns#>
PREFIX dqv: <http://www.w3.org/ns/dqv#>
PREFIX sioc: <http://rdfs.org/sioc/ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

  SELECT ?qualityMetric ?metricLabel (COUNT(?violationResult) AS ?numberViolations)
  WHERE {
    ?violationResult a sh:ValidationResult ;
      sh:sourceShape ?violatedShape ;
      sh:focusNode ?focusNode .


    ?annot a oa:Annotation ;
      oa:motivatedBy bsq:collectionAssessment ;
      oa:hasBody ?violatedShape ;
      oa:hasTarget ?qualityMetric .
    
    ?qualityMetric a dqv:Metric ;
                   skos:prefLabel ?metricLabel .

   #
   # we are only interested in validation results which targeted a collection (not e.g. an observation)
   {
     SELECT ?focusNode
     WHERE {
       SERVICE <db://sfm-collections> {
           ?focusNode a bsm:SocialMediaCollectionRepresentation .
       }
     }
   } 
  } GROUP BY ?qualityMetric ?metricLabel
