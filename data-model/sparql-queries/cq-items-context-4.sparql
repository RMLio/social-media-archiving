PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX http: <http://www.w3.org/2011/http#>
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX edm: <http://www.europeana.eu/schemas/edm/>
PREFIX bsd: <http://example.org/ns/besocial/data#>

SELECT DISTINCT ?collectionName ?ne
WHERE {

  #
  # retrieve named Entities via prov derivations
  #
  ?derivation a prov:Derivation ;
              prov:entity ?namedEntities ;
              prov:hadActivity ?nerActivity .

  ?namedEntities a prov:Collection ;
                 prov:hadMember ?ne .

  ?nerActivity a prov:Activity ;
               dc:description ?componentDescription ;
               prov:used ?itemCHO .

  #
  # retrieve collection via representation, HTTP response and WARC file
  #
  ?itemAggregation a edm:Aggregation ;
                   edm:hasView ?itemRepresentation ;
                   edm:aggregatedCHO ?itemCHO .

  ?itemRepresentation a edm:WebResource ;
                     dcterms:isPartOf ?response .

  ?response a http:Response ;
            dcterms:isPartOf ?warcFile .

  ?warcFile a premis:File ;
            dcterms:isPartOf ?collectionRepresentationVersion .

  ?collectionRepresentationVersion a edm:WebResource ;
                                   prov:specializationOf ?representation .

  ?aggregation a edm:Aggregation ;
               edm:hasView ?representation ;
               edm:aggregatedCHO ?collectionCHO .

  ?collectionCHO a edm:ProvidedCHO ;
                 dc:title ?collectionName .
}
